// Requires gradle 6.5
// gradle clean build

plugins {
    id 'java'
}

repositories {
    mavenCentral()
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
}

sourceCompatibility = '11'
targetCompatibility = '11'

sourceSets {
    main {
        java {
            srcDirs += ["src/gen/java"]
        }
    }
}

jar { 
    manifest { 
        attributes 'Implementation-Version': "${rootProject.version}",
                   'Class-Path': configurations.compile.collect { it.getName() }.join(' ')
    }
}

java {
    modularity.inferModulePath = true
}

javadoc {
    options.noTimestamp = true;
    options.noQualifiers = ["all"];
    options.noTree = true;
    options.noIndex = true;
    options.noNavBar = true;
}

def packageDir = "$buildDir/package/${projectName}"

ext.artifacts = fileTree(include: ['*'], dir: "$packageDir/libs")

task createRelease(type: Zip) {
    archiveFileName = "$releaseFileName"
    destinationDirectory = file("$projectDir/release")
    from file("$packageDir/..")
}

import java.nio.file.Files;
import java.nio.file.Paths;
task copyNativeLibs(type: Copy) {
    def iter = Files.newDirectoryStream(Paths.get("$projectDir/bld"), "librealsense2-jni-*")
        .iterator();
    if (!iter.hasNext())
        throw new RuntimeException("No librealsense2-jni library found. Please build it first. See build.txt for more information.");
    from iter.next()
    into "${projectDir}/libs"
}

task copyLibs(type: Copy, dependsOn: copyNativeLibs) {
    from "$projectDir/libs"
    from jar.archiveFile
    into "${packageDir}/libs"
}

task copyReadme(type: Copy) {
    from("${project.rootDir}/README.md")
    into("$projectDir/src/main/resources")
}

task createPackage(type: Copy, dependsOn: copyLibs) {
    from "${project.rootDir}/README.md"
    from "${project.rootDir}/LICENSE-2.0.txt"
    into packageDir
}

task cleanRelease(type: Delete) {
    delete "$packageDir"
    delete "$projectDir/release"
}

compileJava.dependsOn copyReadme
createRelease.dependsOn createPackage, javadoc
assemble.dependsOn createRelease
clean.dependsOn cleanRelease
